# -----------------------------------------------------------------------------
# Dependencies.cmake
# ~~~
#
# Resolves third-party libraries: Kokkos and Heffte.
#
# Not responsible for:
#   - Selecting platform backends            → Platforms.cmake
#   - Enabling compiler flags                → CompilerOptions.cmake
#   - Version variables or target creation   → Version.cmake / src/
#
# ~~~
# -----------------------------------------------------------------------------
set(FETCHCONTENT_BASE_DIR "${PROJECT_BINARY_DIR}/_deps")
set(FETCHCONTENT_UPDATES_DISCONNECTED ON) # opt out of auto-updates
set(FETCHCONTENT_QUIET ON)

include(FetchContent)
include(CheckCCompilerFlag)
include(LapackDependency)

# ------------------------------------------------------------------------------
# MPI
# ------------------------------------------------------------------------------
find_package(MPI COMPONENTS CXX REQUIRED)
colour_message(STATUS ${Green} "✅ MPI found ${MPI_CXX_VERSION}")

# ------------------------------------------------------------------------------
# CUDA
# ------------------------------------------------------------------------------
if("CUDA" IN_LIST IPPL_PLATFORMS)
  find_package(CUDAToolkit REQUIRED)
  colour_message(STATUS ${Green}
                 "✅ CUDA platform requested and CUDAToolkit found ${CUDAToolkit_VERSION}")
endif()

# ------------------------------------------------------------------------------
# OpenMP
# ------------------------------------------------------------------------------
if("OPENMP" IN_LIST IPPL_PLATFORMS)
  find_package(OpenMP REQUIRED)
  colour_message(STATUS ${Green} "✅ OpenMP platform requested OpenMP found ${OPENMP_VERSION}")
endif()

# ------------------------------------------------------------------------------
# Utility function to clear a list of vars one by one
# ------------------------------------------------------------------------------
function(unset_vars)
  foreach(VAR IN LISTS ARGN)
    unset(${VAR} PARENT_SCOPE)
  endforeach()
endfunction()

# ------------------------------------------------------------------------------
# Utility function to get git tag/sha/version from version string
# ------------------------------------------------------------------------------
function(extract_git_label VERSION_STRING RESULT_VAR)
  if("${${VERSION_STRING}}" MATCHES "^git\\.(.+)$")
    set(${RESULT_VAR} "${CMAKE_MATCH_1}" PARENT_SCOPE)
  else()
    unset(${RESULT_VAR} PARENT_SCOPE)
  endif()
endfunction()

# ------------------------------------------------------------------------------
# Utility function to get git tags from a repo before downloading (unused currently)
# ------------------------------------------------------------------------------
function(get_git_tags GIT_REPOSITORY RESULT_VAR)
  message("Fetching git tags for repo ${GIT_REPOSITORY}")
  execute_process(
    COMMAND git -c versionsort.suffix=- ls-remote --tags --sort=v:refname ${GIT_REPOSITORY}
    COMMAND cut --delimiter=/ --fields=3
    COMMAND grep -Po "^[\\d.]+$"
    OUTPUT_VARIABLE GIT_TAGS
    OUTPUT_STRIP_TRAILING_WHITESPACE)

  # Convert the output string into a CMake list
  string(REPLACE "\n" ";" GIT_TAGS_LIST "${GIT_TAGS}")
  set(${RESULT_VAR} "${GIT_TAGS_LIST}" PARENT_SCOPE)
endfunction()

# ------------------------------------------------------------------------------
# IPPL library
# ------------------------------------------------------------------------------
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_EXTENSIONS OFF)

if ("${IPPL_PLATFORMS}" STREQUAL "CUDA")
    set(CMAKE_CXX_FLAGS_DEBUG "${CMAKE_CXX_FLAGS_DEBUG} -Wno-deprecated-gpu-targets")
endif()

# Disable compile time assert (used by IPPL)
add_definitions (-DNOCTAssert)

# Allow user to specify branch/tag, default to master. Numeric release versions such as
# 3.2.0 are accepted as shorthand for IPPL-3.2.0.
# The Barnes-Hut solver needs the NBody module, which lives on the ippl-bh fork for now.
if(OPALX_ENABLE_BH)
    set(_opalx_ippl_default_repository "https://github.com/tischwab0911/ippl-bh.git")
    set(_opalx_ippl_default_tag "opalx-bh")
else()
    set(_opalx_ippl_default_repository "https://github.com/IPPL-framework/ippl.git")
    set(_opalx_ippl_default_tag "master")
endif()
set(IPPL_GIT_REPOSITORY "${_opalx_ippl_default_repository}" CACHE STRING "Git repository for IPPL")
set(IPPL_GIT_TAG "${_opalx_ippl_default_tag}" CACHE STRING "Branch, tag, commit, or release version for IPPL")
set(_opalx_ippl_fetch_ref "${IPPL_GIT_TAG}")
if("${_opalx_ippl_fetch_ref}" MATCHES "^[0-9]+(\\.[0-9]+)*$")
    set(_opalx_ippl_fetch_ref "IPPL-${_opalx_ippl_fetch_ref}")
endif()
string(REGEX MATCH "^[0-9a-fA-F]+$" _opalx_ippl_fetch_ref_hex "${_opalx_ippl_fetch_ref}")
string(LENGTH "${_opalx_ippl_fetch_ref}" _opalx_ippl_fetch_ref_length)
set(_opalx_ippl_git_shallow TRUE)
if(_opalx_ippl_fetch_ref_hex AND _opalx_ippl_fetch_ref_length EQUAL 40)
    set(_opalx_ippl_git_shallow FALSE)
endif()

if("${_opalx_ippl_fetch_ref}" STREQUAL "${IPPL_GIT_TAG}")
    message(STATUS "Fetching IPPL ref: ${_opalx_ippl_fetch_ref}")
else()
    message(STATUS "Fetching IPPL ref: ${_opalx_ippl_fetch_ref} (from IPPL_GIT_TAG=${IPPL_GIT_TAG})")
endif()
message(STATUS "IPPL repository: ${IPPL_GIT_REPOSITORY}")
message(STATUS "IPPL shallow fetch: ${_opalx_ippl_git_shallow}")

if (NOT CMAKE_BUILD_TYPE AND NOT CMAKE_CONFIGURATION_TYPES)
    set(CMAKE_BUILD_TYPE Debug CACHE STRING "Choose build type" FORCE)
endif()
message(STATUS "Build type is: ${CMAKE_BUILD_TYPE}")

FetchContent_Declare(
    IPPL
    GIT_REPOSITORY "${IPPL_GIT_REPOSITORY}"
    GIT_TAG "${_opalx_ippl_fetch_ref}"
    GIT_SHALLOW ${_opalx_ippl_git_shallow}
    DOWNLOAD_EXTRACT_TIMESTAMP TRUE
)

# Exact SHA pins may require fetching objects that are not present in an existing checkout.
set(_opalx_fetchcontent_updates_disconnected "${FETCHCONTENT_UPDATES_DISCONNECTED}")
if(NOT _opalx_ippl_git_shallow)
    set(FETCHCONTENT_UPDATES_DISCONNECTED OFF)
endif()


# Kokkos 5 removed these toggles. Stale cache entries or old site presets can make
# Kokkos fail during option validation, so remove them before IPPL adds Kokkos.
foreach(_opalx_removed_kokkos_option
        Kokkos_ENABLE_DEBUG_DUALVIEW_MODIFY_CHECK
        Kokkos_ENABLE_CUDA_UVM
        Kokkos_ENABLE_CUDA_LAMBDA
        Kokkos_ENABLE_CUDA_LDG_INTRINSIC)
    if(DEFINED ${_opalx_removed_kokkos_option})
        unset(${_opalx_removed_kokkos_option})
        unset(${_opalx_removed_kokkos_option} CACHE)
    endif()
endforeach()




FetchContent_MakeAvailable(IPPL)
set(FETCHCONTENT_UPDATES_DISCONNECTED "${_opalx_fetchcontent_updates_disconnected}")
message(STATUS "IPPL fetched ref: ${_opalx_ippl_fetch_ref}")
message(STATUS "IPPL include path: ${IPPL_SOURCE_DIR}/src")

# set(IPPL_INCLUDE_DIR "${IPPL_SOURCE_DIR}/src")
set(IPPL_LIBRARY ippl)

message(STATUS "Found IPPL_DIR: ${IPPL_DIR}")

# ------------------------------------------------------------------------------
# HDF5
# ------------------------------------------------------------------------------
if(OPALX_USE_INSTALLED_HDF5)

    message(STATUS "⚙ Using system-installed HDF5")

    # Require parallel HDF5 with C++ components disabled (we only use C libs)
    find_package(HDF5 REQUIRED COMPONENTS C)

    if(NOT HDF5_FOUND)
        message(FATAL_ERROR "System HDF5 requested but not found.")
    endif()

    if (HDF5_VERSION VERSION_LESS "1.10.0")
    message(FATAL_ERROR
        "System HDF5 version ${HDF5_VERSION} is too old. "
        "Required version is >= 1.10.0")
    endif()

    # Normalize your variable names
    set(HDF5_INCLUDE_DIR ${HDF5_INCLUDE_DIRS})
    set(HDF5_LIBRARIES   ${HDF5_LIBRARIES})

    message(STATUS "✔ Found system HDF5 version: ${HDF5_VERSION}")
    message(STATUS "✔ HDF5 include dir: ${HDF5_INCLUDE_DIR}")

else()
    message(STATUS "⚙ Building HDF5 from source (FetchContent)")
    set(HDF5_ENABLE_PARALLEL ON)
    set(HDF5_BUILD_HL_LIB OFF CACHE BOOL “” FORCE) # Disable high-level APIs for thread safety
    set(HDF5_BUILD_EXAMPLES OFF CACHE BOOL “” FORCE) # Disable examples
    set(HDF5_BUILD_TOOLS OFF CACHE BOOL “” FORCE) # Disable tools
    set(HDF5_ENABLE_THREADSAFE OFF CACHE BOOL “” FORCE)
    set(HDF5_TEST_PARALLEL OFF)
    set(HDF5_VERSION "2.2.0")

    if(HDF5_VERSION VERSION_LESS "2.0.0")
        set(HDF5_RELEASE_TAG "hdf5_${HDF5_VERSION}")
    else()
        set(HDF5_RELEASE_TAG "${HDF5_VERSION}")
    endif()

    if(HDF5_VERSION STREQUAL "1.14.6")
        set(HDF5_URL_HASH "SHA256=e4defbac30f50d64e1556374aa49e574417c9e72c6b1de7a4ff88c4b1bea6e9b")
    elseif(HDF5_VERSION STREQUAL "2.2.0")
        set(HDF5_URL_HASH "SHA256=1a1ab8209b35586fbc1aa279ba76d102130b95badcb20ca329587219112d8c16")
    else()
        message(FATAL_ERROR
            "No SHA256 hash is configured for HDF5 ${HDF5_VERSION}. "
            "Add the release tarball hash to cmake/Dependencies.cmake.")
    endif()

    set(HDF5_ENABLE_FLOAT16 OFF CACHE BOOL "Disable half-precision floats" FORCE)
    FetchContent_Declare(
        HDF5
        URL https://github.com/HDFGroup/hdf5/releases/download/${HDF5_RELEASE_TAG}/hdf5-${HDF5_VERSION}.tar.gz
        URL_HASH ${HDF5_URL_HASH}
    )

    # Now FetchContent_MakeAvailable will see the option
    FetchContent_MakeAvailable(HDF5)

    # HDF5 2.2.0 triggers this Clang diagnostic in H5Dint.c. Keep the
    # suppression private to the bundled dependency so OPALX retains it,
    # and only enable it for Clang variants that recognize the option.
    if(CMAKE_C_COMPILER_ID MATCHES "Clang")
        check_c_compiler_flag(
            "-Werror=unknown-warning-option -Wno-uninitialized-const-pointer"
            OPALX_C_SUPPORTS_WNO_UNINITIALIZED_CONST_POINTER)
        if(OPALX_C_SUPPORTS_WNO_UNINITIALIZED_CONST_POINTER)
            foreach(_opalx_hdf5_target hdf5-static hdf5-shared)
                if(TARGET ${_opalx_hdf5_target})
                    target_compile_options(
                        ${_opalx_hdf5_target}
                        PRIVATE -Wno-uninitialized-const-pointer)
                endif()
            endforeach()
        endif()
    endif()

    set(HDF5_FOUND TRUE)

    if (TARGET hdf5-shared)
        add_library(hdf5::hdf5 ALIAS hdf5-shared)
    elseif(TARGET hdf5-static)
        add_library(hdf5::hdf5 ALIAS hdf5-static)
    endif()

    set(HDF5_LIBRARIES hdf5::hdf5)
    message ("HDF5_FOUND and dir are ${HDF5_FOUND} ${HDF5_BINARY_DIR} and ${HDF5_LIBRARIES} and ${HDF5_VERSION}")
endif()

message(STATUS "HDF5 libraries: ${HDF5_LIBRARIES}")
message(STATUS "HDF5 include dir: ${HDF5_INCLUDE_DIR}")

# ------------------------------------------------------------------------------
# H5hut
# ------------------------------------------------------------------------------
if(OPALX_USE_INSTALLED_H5HUT)
    message(STATUS "⚙ Using system-installed H5hut")

    # If you know the module set these env variables
    if(DEFINED ENV{H5HUT_INCLUDE_DIR} AND DEFINED ENV{H5HUT_LIBRARY_DIR})
        set(H5HUT_INCLUDE_DIRS $ENV{H5HUT_INCLUDE_DIR})
        set(H5HUT_LIBRARIES    $ENV{H5HUT_LIBRARY_DIR}/libh5hut.so)
        message(STATUS "✔ Using H5hut include: ${H5HUT_INCLUDE_DIRS}")
        message(STATUS "✔ Using H5hut library: ${H5HUT_LIBRARIES}")
    endif()

    # Prefer config mode first
    find_package(H5hut QUIET CONFIG)

    if(NOT H5hut_FOUND)
        # fallback to module mode if someone uses FindH5hut.cmake
        find_package(H5hut REQUIRED MODULE)
    endif()

    if(NOT H5hut_FOUND)
        message(FATAL_ERROR "System H5hut requested, but not found.")
    endif()

    message(STATUS "✔ Found system H5Hut")

    # Normalize variables for downstream use
    set(H5HUT_INCLUDE_DIR ${H5HUT_INCLUDE_DIRS} CACHE PATH "H5hut include dir")
    set(H5HUT_LIBRARY     ${H5HUT_LIBRARIES}    CACHE FILEPATH "H5hut library")
else()
    message(STATUS "⚙ Building H5Hut from source (FetchContent)")
    set(H5hut_VERSION cmake)
    set(H5hut_GIT https://github.com/H5hut/H5hut.git)
    set(H5hut_WITH_MPI ON)
    set(fetch_string
      GIT_TAG ${H5hut_VERSION}
      GIT_REPOSITORY ${H5hut_GIT})

    # Invoke cmake fetch/find
    FetchContent_Declare(H5hut ${fetch_string})
    set(OPALX_PREVIOUS_CMAKE_SKIP_INSTALL_RULES ${CMAKE_SKIP_INSTALL_RULES})
    set(CMAKE_SKIP_INSTALL_RULES TRUE)
    FetchContent_MakeAvailable(H5hut)
    set(CMAKE_SKIP_INSTALL_RULES ${OPALX_PREVIOUS_CMAKE_SKIP_INSTALL_RULES})
    unset(OPALX_PREVIOUS_CMAKE_SKIP_INSTALL_RULES)

    # Keep warnings enabled for OPALX while suppressing diagnostics from the fetched
    # third-party H5hut sources. Installed H5hut libraries are not compiled here.
    if(TARGET H5hut)
      target_compile_options(H5hut PRIVATE
        $<$<COMPILE_LANG_AND_ID:C,GNU,Clang,AppleClang,NVHPC>:-w>
        $<$<COMPILE_LANG_AND_ID:C,MSVC>:/w>)
    endif()

    # Check that kokkos actually has the platform backends that we need
    if (H5hut_FOUND)
      message(STATUS "H5hut ${H5hut_VERSION} found externally")
    else()
      message(STATUS "H5hut ${H5hut_VERSION} building from source in ${H5hut_SOURCE_DIR}")
      set(H5hut_FOUND ON)
    endif()

    # The H5Hut CMake project itself creates the target "H5hut"
    # and sets these variables:
    #     H5HUT_INCLUDE_DIRS
    #     H5HUT_LIBRARIES
    set(H5HUT_INCLUDE_DIR ${H5HUT_INCLUDE_DIRS})
endif()

# Normalize exported variables used by your project
message(STATUS "H5HUT include dir: ${H5HUT_INCLUDE_DIR}")

# ------------------------------------------------------------------------------
# GoogleTest
# ------------------------------------------------------------------------------
if(OPALX_ENABLE_UNIT_TESTS)
    if(OPALX_USE_INSTALLED_GTEST)
        message(STATUS "⚙ Using system-installed GoogleTest")

        # 1. Try to find the package (modern CMake creates GTest::gtest_main automatically)
        find_package(GTest REQUIRED)

        # 2. Fallback: If the package was found but targets weren't created (older CMake)
        if(NOT TARGET GTest::gtest_main)
            message(STATUS "⚙ Creating GTest targets manually from variables...")
            
            # fallback if GTest_FOUND not set, but dirs exist
            if(NOT GTest_FOUND AND DEFINED GTEST_LIBRARIES AND DEFINED GTEST_INCLUDE_DIRS)
                set(GTest_FOUND TRUE)
            endif()

            if(NOT GTest_FOUND)
                message(FATAL_ERROR "OPALX_USE_INSTALLED_GTEST=ON but system GTest not found.")
            endif()

            # Normalize variables - handle both single path and list
            if(DEFINED GTEST_INCLUDE_DIRS)
                set(GTest_INCLUDE_DIRS ${GTEST_INCLUDE_DIRS})
            elseif(DEFINED GTEST_INCLUDE_DIR)
                set(GTest_INCLUDE_DIRS ${GTEST_INCLUDE_DIR})
            endif()

            # Extract gtest library (might be a list, take first)
            if(DEFINED GTEST_LIBRARIES)
                list(GET GTEST_LIBRARIES 0 GTEST_LIB_SINGLE)
                set(GTest_LIBRARIES ${GTEST_LIBRARIES})
            elseif(DEFINED GTEST_LIBRARY)
                set(GTEST_LIB_SINGLE ${GTEST_LIBRARY})
                set(GTest_LIBRARIES ${GTEST_LIBRARY})
            else()
                message(FATAL_ERROR "GTest libraries not found in GTEST_LIBRARIES or GTEST_LIBRARY")
            endif()

            message(STATUS "✔ Found system GTest: ${GTest_LIBRARIES}")

            # Create imported target for gtest framework
            if(NOT TARGET GTest::gtest)
                add_library(GTest::gtest UNKNOWN IMPORTED GLOBAL)
                set_target_properties(GTest::gtest PROPERTIES
                    IMPORTED_LOCATION "${GTEST_LIB_SINGLE}"
                    INTERFACE_INCLUDE_DIRECTORIES "${GTest_INCLUDE_DIRS}"
                )
            endif()

            # Find gtest_main library - try multiple approaches
            set(GTEST_MAIN_LIB "")
            
            # Approach 1: Check if GTEST_MAIN_LIBRARIES is set
            if(DEFINED GTEST_MAIN_LIBRARIES)
                list(GET GTEST_MAIN_LIBRARIES 0 GTEST_MAIN_LIB)
            elseif(DEFINED GTEST_MAIN_LIBRARY)
                set(GTEST_MAIN_LIB ${GTEST_MAIN_LIBRARY})
            endif()
            
            # Approach 2: Try to find it in the same directory as gtest
            if(NOT GTEST_MAIN_LIB)
                get_filename_component(GTEST_LIB_DIR "${GTEST_LIB_SINGLE}" DIRECTORY)
                get_filename_component(GTEST_LIB_EXT "${GTEST_LIB_SINGLE}" EXT)
                
                # Only search if we have a valid directory
                if(GTEST_LIB_DIR AND EXISTS "${GTEST_LIB_DIR}")
                    # Try common naming patterns
                    set(_possible_names
                        "${GTEST_LIB_DIR}/libgtest_main${GTEST_LIB_EXT}"
                        "${GTEST_LIB_DIR}/libgtest_main.a"
                        "${GTEST_LIB_DIR}/libgtest_main.so"
                        "${GTEST_LIB_DIR}/gtest_main${GTEST_LIB_EXT}"
                        "${GTEST_LIB_DIR}/gtest_main.a"
                        "${GTEST_LIB_DIR}/gtest_main.so"
                    )
                    
                    foreach(_name IN LISTS _possible_names)
                        if(EXISTS "${_name}")
                            set(GTEST_MAIN_LIB "${_name}")
                            break()
                        endif()
                    endforeach()
                endif()
            endif()
            
            # Approach 3: Use find_library as last resort (in same directory)
            if(NOT GTEST_MAIN_LIB AND GTEST_LIB_DIR AND EXISTS "${GTEST_LIB_DIR}")
                find_library(GTEST_MAIN_LIB_FOUND
                    NAMES gtest_main libgtest_main
                    PATHS ${GTEST_LIB_DIR}
                    NO_DEFAULT_PATH
                )
                if(GTEST_MAIN_LIB_FOUND)
                    set(GTEST_MAIN_LIB ${GTEST_MAIN_LIB_FOUND})
                endif()
            endif()

            # Create gtest_main target if found
            if(GTEST_MAIN_LIB AND EXISTS "${GTEST_MAIN_LIB}")
                if(NOT TARGET GTest::gtest_main)
                    add_library(GTest::gtest_main UNKNOWN IMPORTED GLOBAL)
                    set_target_properties(GTest::gtest_main PROPERTIES
                        IMPORTED_LOCATION "${GTEST_MAIN_LIB}"
                        INTERFACE_LINK_LIBRARIES "GTest::gtest"   # crucial!
                        INTERFACE_INCLUDE_DIRECTORIES "${GTest_INCLUDE_DIRS}"
                    )
                endif()
                message(STATUS "✔ Found gtest_main library: ${GTEST_MAIN_LIB}")
            else()
                message(FATAL_ERROR 
                    "GTest::gtest_main target not found and could not locate gtest_main library.\n"
                    "Searched in: ${GTEST_LIB_DIR}\n"
                    "Please ensure gtest_main is installed or set GTEST_MAIN_LIBRARIES manually.")
            endif()

        endif()

        message(STATUS "✅ System GTest targets ready.")

    else()
        message(STATUS "⚙ Building GoogleTest from source (FetchContent)")

        FetchContent_Declare(GTest GIT_REPOSITORY "https://github.com/google/googletest"
                            GIT_TAG "v1.16.0" GIT_SHALLOW ON)

        # For Windows: force shared crt, ignored on linux
        set(gtest_force_shared_crt ON CACHE BOOL "" FORCE)

        # Turn off GTest install/tests in the subproject
        set(INSTALL_GTEST OFF CACHE BOOL "" FORCE)
        set(BUILD_GMOCK OFF CACHE BOOL "" FORCE)
        set(BUILD_GTEST ON CACHE BOOL "" FORCE)

        FetchContent_MakeAvailable(GTest)

        # GoogleTest 1.16.0 triggers this Clang diagnostic in
        # gtest-printers.h. Do not weaken warnings on OPALX targets.
        if(CMAKE_CXX_COMPILER_ID MATCHES "Clang")
            foreach(_opalx_gtest_target gtest gtest_main)
                if(TARGET ${_opalx_gtest_target})
                    target_compile_options(
                        ${_opalx_gtest_target}
                        PRIVATE -Wno-character-conversion)
                endif()
            endforeach()
        endif()

        message(STATUS "✅ GoogleTest built from source (${GTest_VERSION})")
    endif()
endif()
# ------------------------------------------------------------------------------

# ------------------------------------------------------------------------------
# ZLIB
# ------------------------------------------------------------------------------
find_package(ZLIB REQUIRED)

if(TARGET ZLIB::ZLIB)
    # Get the actual library path from the target
    get_target_property(ZLIB_PATH ZLIB::ZLIB IMPORTED_LOCATION)
    
    # If IMPORTED_LOCATION is empty (common for shared system libs), 
    # fall back to the variable find_package usually sets
    if(NOT ZLIB_PATH)
        set(ZLIB_PATH ${ZLIB_LIBRARIES})
    endif()

    message(STATUS "✅ ZLIB linked via target ZLIB::ZLIB (${ZLIB_PATH})")
else()
    message(WARNING "⚠️ ZLIB::ZLIB target was not created!")
endif()
